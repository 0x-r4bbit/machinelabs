isExecutionServer() {
  return auth.uid === 'execution-server';
}

isAssignServerFn() {
  return auth.uid === 'cloud-fn-assign-server';
}

isCurrentUser(uid) { auth != null && auth.uid == uid }

isAnonymous() {
  return auth.provider === 'anonymous';
}

isOwningUserOrExecutionServerOrAssignFn(ref) {
  return prior(ref) !== null ? (isCurrentUser(data.child('user_id').val()) && isCurrentUser(newData.child('user_id').val())) || isExecutionServer() || isAssignServerFn() : isCurrentUser(newData.child('user_id').val())
}

isOwningUser(ref, uidFieldName) {
  return prior(ref) !== null ? (isCurrentUser(data.child(uidFieldName).val()) && isCurrentUser(newData.child(uidFieldName).val())) : isCurrentUser(newData.child(uidFieldName).val())
}

type Lab {
  id: String,
  user_id: String,
  name: String,
  description: String,
  has_cached_run: Boolean | Null,
  file_set_hash: String | Null
  tags: String[],
  files: File[]
}

type File {
  name: String,
  content: String
}

type InvocationLab {
  id: String,
  files: File[]
}

type AllowOther {
  validate() { true }
}

type Invocation {
  id: String,
  user_id: String,
  timestamp: Number,
  type: Number,
  data: InvocationLab
}

type Execution {
  id: String,
  file_set_hash: String,
  started_at: Number,
  finished_at: Number | Null,
  server_info: String,
  user_id: String,
  lab_id: String,
  status: Number
}

type ExecutionMessage {
  id: String,
  timestamp: Number,
  data: String,
  kind: Number
}

type AnonymousState extends Boolean {
  validate() { this === isAnonymous() }
}

type User {
  id: String,
  isAnonymous: AnonymousState,
  displayName: String,
  photoUrl: String,
  email: String,
}

type Server {
  id: String,
  hardware_type: String,
  name: String
}

path / {
  read() { isExecutionServer() }
  write() { false }
}

path /labs/{id}/common is Lab {
  read() { true }
  write() { isOwningUserOrExecutionServerOrAssignFn(this) }
}

path /labs {
  index() { ['common/file_set_hash'] }
}

path /invocations/{id}/common is Invocation {
  read() { isCurrentUser(data.child('user_id').val()) || isAssignServerFn() }
  write() { isOwningUserOrExecutionServerOrAssignFn(this) }
}

path /invocations/{id}/server {
  read() { isAssignServerFn() || isExecutionServer() }
  write() { isAssignServerFn() }
}

path /invocations {
  index() { ['server/id'] }
}

path /executions/{id}/common is Execution {
  read() { true }
  write() { isExecutionServer() }
}

path /executions {
  index() { ['common/file_set_hash']}
}

path /executions/{id}/messages {
  read() { true }
  write() { isExecutionServer() }
}

path /executions/{id}/messages/{mid} is ExecutionMessage {
  read() { true }
  write() { isExecutionServer() }
}

path /users/{id}/common is User {
  read() { true }
  write() { isOwningUser(this, 'id') }
}

path /servers {
  read() { isExecutionServer() || isAssignServerFn() }
  write() { false }
}

path /servers/{id} is Server {}

path /user_labs/{id} {
  read() { true }
  write() { false }
}
